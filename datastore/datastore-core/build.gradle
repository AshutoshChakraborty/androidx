/*
 * Copyright (C) 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.Publish
import org.jetbrains.kotlin.gradle.plugin.mpp.KotlinNativeTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinNativeCompile

plugins {
    id("AndroidXPlugin")
    id("org.jetbrains.kotlin.multiplatform")
}


def boolProperty(name) {
    return project.providers.gradleProperty(name).getOrNull()?.toBoolean()
}

def playground = androidx.build.StudioType.isPlayground(project)
//Native on by default in playground
def enableNative = boolProperty('androidx.kmp.native.enabled') ?: playground


kotlin {
    jvm {
        withJava()
    }
    if (enableNative) {
        macosArm64()
    }

    sourceSets {
        commonMain {
            dependencies {
                api(libs.atomicFu)
                api(libs.kotlinStdlib)
                api(libs.kotlinCoroutinesCore)
                implementation("com.squareup.okio:okio:3.0.0")
            }
        }
        commonTest {
            dependencies {
                implementation(libs.kotlinTestCommon)
                implementation(libs.kotlinTestAnnotationsCommon)
                implementation(libs.kotlinCoroutinesTest)
                implementation("com.squareup.okio:okio-fakefilesystem:3.0.0")
            }
        }

        if (enableNative) {
            nativeMain

            configure([macosArm64Main]) {
                dependsOn nativeMain
            }

            nativeTest
            configure([macosArm64Test]) {
                dependsOn nativeTest
            }
        }

        jvmMain {
            dependencies {
                api(libs.kotlinStdlib)
                api(libs.kotlinCoroutinesCore)
                api("androidx.annotation:annotation:1.1.0")
            }
        }
        jvmTest {
            dependencies {
                implementation(libs.junit)
                implementation(libs.kotlinCoroutinesTest)
                implementation(libs.truth)
                implementation(project(":internal-testutils-truth"))
                implementation(libs.kotlinTest)
                implementation(libs.kotlinTestAnnotationsCommon)

            }
        }
    }
    targets.withType(KotlinNativeTarget).configureEach {
        binaries.all {
            binaryOptions["memoryModel"] = "experimental"
        }
    }
}

androidx {
    name = "Android DataStore Core"
    publish = Publish.SNAPSHOT_AND_RELEASE
    mavenGroup = LibraryGroups.DATASTORE
    inceptionYear = "2020"
    description = "Android DataStore Core - contains the underlying store used by each serialization method"
    legacyDisableKotlinStrictApiMode = true
    publishPlatforms = ["jvm"]
}

// Allow usage of Kotlin's @OptIn.
tasks.withType(KotlinNativeCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]
    }
}
