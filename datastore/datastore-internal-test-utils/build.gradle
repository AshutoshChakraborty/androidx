/*
 * Copyright (C) 2020 The Android Open Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import androidx.build.LibraryType
import androidx.build.Publish
import org.jetbrains.kotlin.gradle.plugin.KotlinPlatformType
import org.jetbrains.kotlin.gradle.tasks.KotlinNativeCompile
import org.jetbrains.kotlin.gradle.plugin.mpp.KotlinNativeTarget

plugins {
    id("AndroidXPlugin")
    id("org.jetbrains.kotlin.multiplatform")
}

kotlin {
    jvm {
        withJava()
    }
    macosX64()
//    linuxArm64()
    macosArm64()
    mingwX64()
    sourceSets {
        commonMain {
            dependencies {
                api(libs.kotlinStdlib)
                api(libs.kotlinTestCommon)
                api(libs.kotlinTestAnnotationsCommon)
                api("com.squareup.okio:okio:3.0.0")
                api(project(":datastore:datastore-core-kmp"))
            }
        }
        commonTest

        jvmMain {
            dependsOn(commonMain)
            dependencies {
                implementation(libs.kotlinTest)
                implementation(libs.kotlinTestAnnotationsCommon)
            }
        }
        jvmTest {
            dependsOn(commonTest)
            dependencies {
                implementation(libs.kotlinTest)
                implementation(libs.kotlinTestAnnotationsCommon)
            }

        }
        nativeMain {
            dependsOn(commonMain)
        }
        nativeTest {
            dependsOn(commonTest)
            dependencies {
                api(project(":datastore:datastore-core-kmp"))
                api("com.squareup.okio:okio:3.0.0")
            }
        }
    }
    targets.forEach {target ->
        if (target.platformType == KotlinPlatformType.native) {
            target.compilations["main"].defaultSourceSet {
                dependsOn(sourceSets["nativeMain"])
            }
            target.compilations["test"].defaultSourceSet {
                dependsOn(sourceSets["nativeTest"])
            }
        }
    }
    targets.withType(KotlinNativeTarget).configureEach {
        binaries.all {
            binaryOptions["memoryModel"] = "experimental"
        }
    }
}

androidx {
    name = "Android DataStore Internal Test Utils"
    type = LibraryType.INTERNAL_TEST_LIBRARY
    publish = Publish.NONE
    inceptionYear = "2022"
    description = "Internal test utils for datastore."
    legacyDisableKotlinStrictApiMode = true
}

// Allow usage of Kotlin's @OptIn.
tasks.withType(KotlinNativeCompile).configureEach {
    kotlinOptions {
        freeCompilerArgs += ["-opt-in=kotlin.RequiresOptIn"]
    }
}
